\section{Fortran 77 Basics}

A Fortran program is just a sequence of lines of text. The text has to
follow a certain \textit{structure} to be a valid Fortran program. We
start by looking at a simple example:

\begin{fortran77}
      program circle
      real r, area

c This program reads a real number r and prints
c the area of a circle with radius r.

      write (*,*) 'Give radius r:'
      read  (*,*) r
      area = 3.14159*r*r
      write (*,*) 'Area = ', area

      stop
      end
\end{fortran77}

The lines that begin with with a ``\inlinefortranss{c}'' are
\textit{comments} and have no purpose other than to make the program
more readable for humans. Originally, all Fortran programs had to be
written in all upper-case letters. Most people now write lower-case
since this is more legible, and so will we. You may wish to mix case,
but Fortran is not case-sensitive, so ``\inlinefortranss{X}'' and
``\inlinefortranss{x}'' are the same variable.


\subsection*{Program Organization}

A Fortran program generally consists of a main program (or driver) and
possibly several subprograms (procedures or subroutines). For now we
will place all the statements in the main program; subprograms will be
treated later. The structure of a main program is:

\begin{fortran77}
      program <<name>>

      <<declarations>>

      <<statements>>

      stop
      end
\end{fortran77}

In this tutorial, words that are in \textit{italics} should not be taken
as literal text, but rather as a description of what belongs in their
place.

The \inlinefortranss{stop} statement is optional and may seem
superfluous since the program will stop when it reaches the end anyway,
but it is recommended to always terminate a program with the stop
statement to emphasize that the execution flow stops there.

You should note that you cannot have a variable with the same name as
the program.


\subsection*{Column Position Rules}

Fortran 77 is \textit{not} a free-format language, but has a very strict
set of rules for how the source code should be formatted. The most
important rules are the column position rules:

\begin{xminicode}
Col. 1    : Blank, or a "c" or "*" for comments
Col. 1-5  : Statement label (optional)
Col. 6    : Continuation of previous line (optional)
Col. 7-72 : Statements
Col. 73-80: Sequence number (optional, rarely used today)
\end{xminicode}

Most lines in a Fortran 77 program starts with 6 blanks and ends before
column 72, i.e. only the statement field is used.


\subsection*{Comments}

A line that begins with the letter ``\inlinefortranss{c}'' or an
asterisk in the first column is a comment. Comments may appear anywhere
in the program. Well-written comments are crucial to program
readability. Commercial Fortran codes often contain about 50\% comments.
You may also encounter Fortran programs that use the exclamation mark
(\inlinefortranss{!}) for comments. This is not a standard part of
Fortran 77, but is supported by several Fortran 77 compilers and is
explicitly allowed in Fortran 90. When understood, the exclamation mark
may appear anywhere on a line (except in positions 2-6).


\subsection*{Continuation}

Sometimes, a statement does not fit into the 66 available columns of a
single line. One can then break the statement into two or more lines,
and use the continuation mark in position 6. Example:

\begin{fortran77}
c23456789 (This demonstrates column position!)

c The next statement goes over two physical lines
      area = 3.14159265358979
     +       * r * r
\end{fortran77}

Any character can be used instead of the plus sign as a continuation
character. It is considered good programming style to use either the
plus sign, an ampersand, or digits (using 2 for the second line, 3 for
the third, and so on).


\subsection*{Blank spaces}

Blank spaces are ignored in Fortran 77. So if you remove all blanks in a
Fortran 77 program, the program is still acceptable to a complier but
almost unreadable to humans.
